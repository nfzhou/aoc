% https://adventofcode.com/2024/day/14, part-1
import util.

main([File]) =>
    Lines = read_file_lines(File),
    proc(Lines,Robots),
    W = 101,
    H = 103,
    N = 100,
    Ps = [{R,C} : {R0,C0,Vr,Vc} in Robots,   % positions of robots after N seconds
          R is (R0+N*Vr) mod H,
          C is (C0+N*Vc) mod W],
    Mc = W div 2,
    Mr = H div 2,
    Q1 = 0, Q2 = 0, Q3 = 0, Q4 = 0,
    foreach ({R,C} in Ps)
        if R < Mr && C < Mc then
            Q1 := Q1+1
        elseif R < Mr && C > Mc then
            Q2 := Q2+1
        elseif R > Mr && C > Mc then
            Q3 := Q3+1
        elseif R > Mr && C < Mc then
            Q4 := Q4+1
        end
    end,
    println(prod([Q1,Q2,Q3,Q4])).

proc([],Robots) => Robots = [].
proc([Line|Lines],Robots), Line = [p,'='|LineR] =>
    append(Str1,[v,'='|Str2],LineR),
    [C,R] = split(Str1, ","),
    [Vc,Vr] = split(Str2, ","),
    Robots = [{to_int(R), to_int(C), to_int(Vr), to_int(Vc)}|RobotsR],
    proc(Lines,RobotsR).
   
   
   
        

